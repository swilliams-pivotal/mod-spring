/*
 * Copyright 2012 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply from: 'gradle/setup.gradle'

defaultTasks = ['assemble']

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

configurations {
  provided
  testMod
}

sourceSets {
  main {
    compileClasspath = compileClasspath + configurations.provided
  }
  testMod {
    compileClasspath = compileClasspath + configurations.compile + configurations.provided + files('build/classes/main')
    java.srcDir file('src/testMod/java')
    resources.srcDir file('src/testMod/resources')
  }
}

repositories {
  maven { url 'http://repo.springsource.org/release' }
  mavenCentral()
  mavenLocal()
}

dependencies {
  provided "org.vert-x:vertx-core:$vertxVersion"
  provided "org.vert-x:vertx-platform:$vertxVersion"

  compile("org.springframework:spring-beans:$springVersion") { transitive = false }
  compile("org.springframework:spring-core:$springVersion") { transitive = false }
  compile("org.springframework:spring-context:$springVersion") { transitive = false }

  testCompile "cglib:cglib-nodep:2.2"
  testCompile "junit:junit:$junitVersion"
  testCompile "org.mozilla:rhino:$rhinoVersion"

  testCompile "org.springframework:spring-aop:$springVersion"
  testCompile "org.springframework:spring-asm:$springVersion"
  testCompile "org.springframework:spring-expression:$springVersion"
  testCompile "org.springframework:spring-test:$springVersion"

  testCompile "org.vert-x:vertx-junit-annotations:1.0"
  testCompile "org.vert-x:vertx-lang-java:$vertxVersion"
  testCompile "org.vert-x:vertx-lang-rhino:$vertxVersion"
  testCompile("org.vert-x:vertx-testframework:$vertxVersion") {
    transitive = false // don't need all of the other language deps
  }
}

javadoc.classpath += configurations.provided

task copyMod(type:Copy, dependsOn: 'classes') {
  into "build/mod/$modulename-v$version"
  from 'build/classes/main'
  from 'build/resources/main'
  from 'src/main/conf'
  into( 'lib' ) {
    from configurations.compile
  }
}

task copyTestMod(type:Copy, dependsOn: 'testModClasses') {
  into "build/mod/example.test-mod-spring-v1.0"
  from 'build/classes/testMod'
  from 'build/resources/testMod'
  from 'src/testMod/conf'
  into('lib') {
    from configurations.testMod
  }
}

// Package into build/libs/mod.zip
task dist(type: Zip, dependsOn: 'copyMod') {
  group = 'vert.x'
  description = "Assembles a vert.x module in 'mod.zip' format"
  destinationDir = project.file('build/libs')
  archiveName = 'mod.zip'
  from("build/mod") {
    exclude '*example*'
  }
}

task prepareVertxTest(type: Sync, dependsOn: ['copyMod', 'copyTestMod'] ) {
  from 'build/mod'
  into 'build/tmp/mod-test'
}

test {
  classpath = files(classpath.findAll {
    !( it.toString() ==~ /.+build\/(classes|resources)\/(main|testMod)$/ )
  })

  // Some vert.x properties
  systemProperty 'vertx.test.timeout', 15
  systemProperty 'vertx.mods', "$projectDir/build/tmp/mod-test"
  systemProperty 'vertx.version', "$project.version"

  // Show output
  testLogging.showStandardStreams = true
}

test.dependsOn testClasses, copyMod, copyTestMod, prepareVertxTest


task collectDeps(type: Copy) {
  group = 'vert.x'
  description = 'conveniently collect dependencies for other IDEs'
  destinationDir = file("build/deps")
  into("compile") {
    from configurations.compile
  }
  into("test") {
    from configurations.testCompile
  }
}
